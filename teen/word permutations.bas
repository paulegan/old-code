'                  *****      WORD PERMUTATIONS      *****

DECLARE FUNCTION FACT% (X%)
DECLARE FUNCTION INSERT$ (CHAR$, POSTN%, STRG$)
CLS

CONST MAX% = 6
DIM WORD(1, FACT%(MAX%)) AS STRING * MAX, L$(MAX%)

INPUT "WORD TO PERMUTE: ", IN$
IF LEN(IN$) > MAX% THEN PRINT " STRING TOO LONG!!!": END
 FOR LENGTH% = 1 TO LEN(IN$)
  LET L$(LENGTH%) = MID$(IN$, LENGTH%, 1)
 NEXT
LENGTH% = LENGTH% - 1
PRINT " NOW PERMUTING ALL"; FACT%(LENGTH%); "POSSIBILITIES..."

WORD(0, 1) = L$(1) + L$(2)
WORD(0, 2) = L$(2) + L$(1)

FOR N% = 3 TO LENGTH%
 FOR X% = 0 TO N% - 1
  FOR Y% = 1 TO FACT%(N% - 1)
   WORD(N% MOD 2, FACT%(N% - 1) * X% + Y%) = INSERT$(L$(N%), X%, WORD((N% - 1) MOD 2, Y%))
  NEXT Y%
 NEXT X%
NEXT N%


FOR X = 1 TO FACT%(LENGTH%)
 PRINT X; WORD(LENGTH% MOD 2, X)
 IF X MOD 22 = 0 THEN
  PRINT "Press a key to continue..."
  DO
  LOOP WHILE INKEY$ = ""
 END IF
NEXT

END

FUNCTION FACT% (X%)
 TMP% = 1
 FOR Y% = 1 TO X%
  TMP% = TMP% * Y%
 NEXT Y%
 FACT% = TMP%
END FUNCTION

FUNCTION INSERT$ (CHAR$, POSTN%, STRG$)
 TEMPB$ = LEFT$(STRG$, POSTN%)
 TEMPA$ = RIGHT$(STRG$, LEN(STRG$) - POSTN%)
 INSERT$ = TEMPB$ + CHAR$ + TEMPA$
END FUNCTION

